
/* Begin MF */

/* GRAPHICS */

spriteset(spriteset_mf_1_front, "src\gfx\mf_1_front_8bpp.png") {template_8_standard(0,0)}
spriteset(spriteset_mf_1_front_rev, "src\gfx\mf_1_front_8bpp.png") {template_8_reversed(0,0)}

spriteset(spriteset_mf_1_middle, "src\gfx\mf_1_middle_8bpp.png") {template_8_standard(0,0)}

spriteset(spriteset_mf_1_rear, "src\gfx\mf_1_rear_8bpp.png") {template_8_standard(0,0)}
spriteset(spriteset_mf_1_rear_reversed, "src\gfx\mf_1_rear_8bpp.png") {template_8_reversed(0,0)}

spriteset(spriteset_mf_headlights, "src\gfx\mf_headlights.png") {template_8_standard(0,0)}
spriteset(spriteset_mf_marker_lights, "src\gfx\mf_marker_lights.png") {template_8_standard(0,0)}


/* SWITCHES */

/* Sprite stacks */

switch (FEAT_TRAINS, SELF, sw_mf_1_front_with_headlights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_mf_1_front;    // Return the base sprite
    1: return spriteset_mf_headlights;    // Return the overlay
}

switch (FEAT_TRAINS, SELF, sw_mf_1_front_with_marker_lights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_mf_1_front_rev;    // Return the base sprite
    1: return spriteset_mf_marker_lights;    // Return the overlay
}

switch (FEAT_TRAINS, SELF, sw_mf_1_rear_with_headlights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_mf_1_rear_reversed;    // Return the base sprite
    1: return spriteset_mf_headlights;    // Return the overlay
}

switch (FEAT_TRAINS, SELF, sw_mf_1_rear_with_marker_lights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_mf_1_rear;    // Return the base sprite
    1: return spriteset_mf_marker_lights;    // Return the overlay
}

switch (FEAT_TRAINS, SELF, sw_mf_1_front_graphics, position_in_consist) {
	0: return sw_mf_1_front_with_headlights;
	return spriteset_mf_1_front;
}

switch (FEAT_TRAINS, SELF, sw_mf_1_rear_graphics, position_in_consist_from_end) {
	0: return sw_mf_1_rear_with_marker_lights;
	return spriteset_mf_1_rear;
}

switch (FEAT_TRAINS, SELF, sw_mf_graphics, position_in_articulated_veh) {
	0: sw_mf_1_front_graphics;
	1: spriteset_mf_1_middle;
	2: sw_mf_1_rear_graphics;
}

switch (FEAT_TRAINS, SELF, sw_mf_articulated_part, extra_callback_info1) {
	1 .. 2: return item_mf;
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/* ITEM*/

item(FEAT_TRAINS, item_mf, 1) {
	property {
		/* Common properties */
		name: string(STR_NAME_MF);
		climates_available: ALL_CLIMATES;
		introduction_date: date(1989,01,01);
		model_life:	40;
		vehicle_life: 40;
		reliability_decay: 20;
		refittable_cargo_classes: bitmask(CC_PASSENGERS);
		default_cargo_type: PASS;
		cargo_capacity: 50;
		loading_speed: 6;
		/* Train-specific properties */
		sprite_id: SPRITE_ID_NEW_TRAIN; //enable new graphics
		speed: 180 km/h;
		misc_flags: bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
		refit_cost: 0;
		track_type:	RAIL;
		ai_special_flag: AI_FLAG_PASSENGER;
		running_cost_base: RUNNING_COST_STEAM;
		engine_class: ENGINE_CLASS_DIESEL;
		power: 1600 hp;
		weight: 97 ton;
		tractive_effort_coefficient: 0.3;
		length: 8;
	}
	graphics {
		default: sw_mf_graphics;
		colour_mapping: sw_cc_red;
		articulated_part: sw_mf_articulated_part;
		cargo_capacity: return (position_in_articulated_veh == 1) ? 64 : 38;
		purchase_cargo_capacity: 140 / 3;
		cost_factor: 90;
		running_cost_factor: 1800;
		visual_effect_and_powered: (position_in_articulated_veh == 1) ? visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER) : visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 0, ENABLE_WAGON_POWER);
	}
}

/* End MF */
