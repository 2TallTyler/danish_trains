/* *** Begin Header *** */

grf {
	grfid: "TT14";
	name: string(STR_GRF_NAME);
	desc: string(STR_GRF_DESC);
	version: 1;
	min_compatible_version: 0;

	url: string(STR_WEBSITE);
	
	param 0 {
		param_company_colours {
			name: string(STR_PARAM_NAME_CCOLOUR);
			desc: string(STR_PARAM_DESC_CCOLOUR);
			min_value: 0;
			max_value: 1;
			def_value: 0;
			names: {
				0: string(PARAM_CCOLOUR_REALISTIC);
				1: string(PARAM_CCOLOUR_CC);
			};
		}
	}
}

railtypetable {
	RAIL, ELRL
}

cargotable {
	PASS
}

/*
Base costs are tweaked to balance vehicle purchase and running costs using a strategy developed by Andythenorth in his excellent Iron Horse 2 train set. 
This requires changing base costs to give greater range and control, and allow me to borrow values directly from the Iron Horse code reference. Thanks, Andy! :)

Note that all engines use RUNNING_COST_STEAM and all wagons use RUNNING_COST_DIESEL. 
Unlike other base cost overrides, changing vehicle base costs only affects vehicles within that NewGRF, so other vehicle sets will not be affected.
*/

basecost {
	PR_BUILD_VEHICLE_TRAIN: -2;
	PR_BUILD_VEHICLE_WAGON: 1;
	PR_RUNNING_TRAIN_STEAM: -2;
	PR_RUNNING_TRAIN_DIESEL: -4;
}

/* *** End Header *** */


/* Begin Sprite Templates */

/* Fix depot view */
traininfo_y_offset = 2;
train_width_32_px = 1;

/* Recolour switches for realistic or company colours */

switch (FEAT_TRAINS, SELF, sw_cc_red, param_company_colours) {
	0: return palette_2cc(COLOUR_RED, COLOUR_RED);
	1: return base_sprite_2cc + CB_RESULT_COLOUR_MAPPING_ADD_CC;
}

switch (FEAT_TRAINS, SELF, sw_cc_blue, param_company_colours) {
	0: return palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN);
	1: return base_sprite_2cc + CB_RESULT_COLOUR_MAPPING_ADD_CC;
}


template template_8_standard(x, y) {
    //left_x, upper_y, width, height, offset_x, offset_y
	//							more negative to shift left
	//											more negative to shift up
    [ x,		y,		8,		24,		-3,			-12]
    [ x+16,		y,		22,		21,		-15,		-11]
    [ x+46,		y,		32,		14,		-16,		-10]
    [ x+86,		y,		22,		21,		-5,			-11]
    [ x+116,	y,		8,		24,		-3,			-12]
    [ x+132,	y,		22,		21,		-15,		-11]
    [ x+162,	y,		32,		14,		-16,		-10]
    [ x+202,	y,		22,		21,		-5,			-11]
}

/* End Sprite Templates */


/* Begin MF */

/* GRAPHICS */

spriteset(spriteset_mf_1_front, "src\gfx\mf_1_front_8bpp.png") {template_8_standard(0,0)}
spriteset(spriteset_mf_1_middle, "src\gfx\mf_1_middle_8bpp.png") {template_8_standard(0,0)}
spriteset(spriteset_mf_1_rear, "src\gfx\mf_1_rear_8bpp.png") {template_8_standard(0,0)}

spriteset(spriteset_mf_headlights, "src\gfx\mf_headlights.png") {template_8_standard(0,0)}
spriteset(spriteset_mf_marker_lights, "src\gfx\mf_marker_lights.png") {template_8_standard(0,0)}


/* SWITCHES */

switch (FEAT_TRAINS, SELF, sw_mf_articulated_part, extra_callback_info1) {
	1 .. 2: return item_mf;
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/*switch (FEAT_TRAINS, SELF, sw_mf_1_front_graphics, getbits(extra_callback_info1, 8, 8)) {
	0: spriteset_mf_1_front, STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100);
	1: spriteset_mf_headlights, STORE_TEMP(PALETTE_USE_DEFAULT, 0x100);
}*/

switch (FEAT_TRAINS, SELF, sw_mf_graphics, position_in_articulated_veh) {
	0: spriteset_mf_1_front;
	1: spriteset_mf_1_middle;
	2: spriteset_mf_1_rear;
}

/* ITEM*/

item(FEAT_TRAINS, item_mf, 1) {
	property {
		/* Common properties */
		name: string(STR_NAME_MF);
		climates_available: ALL_CLIMATES;
		introduction_date: date(1989,01,01);
		model_life:	40;
		vehicle_life: 40;
		reliability_decay: 20;
		refittable_cargo_classes: bitmask(CC_PASSENGERS);
		default_cargo_type: PASS;
		cargo_capacity: 50;
		loading_speed: 6;
		/* Train-specific properties */
		sprite_id: SPRITE_ID_NEW_TRAIN; //enable new graphics
		speed: 180 km/h;
		misc_flags: bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
		refit_cost: 0;
		track_type:	RAIL;
		ai_special_flag: AI_FLAG_PASSENGER;
		running_cost_base: RUNNING_COST_STEAM;
		engine_class: ENGINE_CLASS_DIESEL;
		power: 1600 hp;
		weight: 97 ton;
		tractive_effort_coefficient: 0.3;
		length: 8;
	}
	graphics {
		default: sw_mf_graphics;
		colour_mapping: sw_cc_red;
		articulated_part: sw_mf_articulated_part;
		cargo_capacity: return (position_in_articulated_veh == 1) ? 64 : 38;
		purchase_cargo_capacity: 140 / 3;
		cost_factor: 90;
		running_cost_factor: 1800;
		visual_effect_and_powered: (position_in_articulated_veh == 1) ? visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER) : visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 0, ENABLE_WAGON_POWER);
	}
}

/* End MF */


/* Begin MZ (IV) */

/* Use real liveries or company colours? (set by parameter) */
switch (FEAT_TRAINS, SELF, switch_cc_mz_iv, param_company_colours == 1)
	{1: spriteset_mz_iv_cc; spriteset_mz_iv;}	

/* Real livery sprites */
spriteset(spriteset_mz_iv, "src\gfx\mz_iv_8bpp.png") {template_8_standard(0,0)}

/* CC sprites */
spriteset(spriteset_mz_iv_cc, "src\gfx\mz_iv_cc_8bpp.png") {template_8_standard(0,0)}

item(FEAT_TRAINS, item_mz_iv, 0) {
	property {
		/* Common properties */
		name: string(STR_NAME_MZ_IV);
		climates_available: ALL_CLIMATES;
		introduction_date: date(1977,01,01);
		model_life:	40; // retired in 2008
		vehicle_life: 40;
		reliability_decay: 20;
		/* Train-specific properties */
		sprite_id: SPRITE_ID_NEW_TRAIN; //enable new graphics
		speed: 103 mph;
		track_type:	RAIL;
		running_cost_base: RUNNING_COST_STEAM;
		engine_class: ENGINE_CLASS_DIESEL;
		power: 3845 hp;
		weight: 123 ton;
		tractive_effort_coefficient: 0.3;
		length: 8;
	}
	graphics {
		default: switch_cc_mz_iv;
		cost_factor: 139;
		running_cost_factor: 5318;
	}
}

/* End MZ (IV) */
