
/* Begin S-Tog 4 */

/* GRAPHICS */

/* Lights */
spriteset(spriteset_stog_4_headlights, "src\gfx\stog_4_headlights.png") {template_4_standard(0,0)}
spriteset(spriteset_stog_4_marker_lights, "src\gfx\stog_4_marker_lights.png") {template_4_reversed(0,0)}

spriteset(spriteset_stog_4_se, "src\gfx\stog_4_se_8bpp.png") {template_4_standard(0,0)}
spriteset(spriteset_stog_4_se_reversed, "src\gfx\stog_4_se_8bpp.png") {template_4_reversed(0,0)}

spriteset(spriteset_stog_4_sb, "src\gfx\stog_4_sb_8bpp.png") {template_4_standard(0,0)}
spriteset(spriteset_stog_4_sb_reversed, "src\gfx\stog_4_sb_8bpp.png") {template_4_reversed(0,0)}

spriteset(spriteset_stog_4_sc, "src\gfx\stog_4_sc_8bpp.png") {template_4_standard(0,0)}
spriteset(spriteset_stog_4_sc_reversed, "src\gfx\stog_4_sc_8bpp.png") {template_4_reversed(0,0)}

/* SWITCHES */

/* Sprite stack for stog_4_se with headlights */
switch (FEAT_TRAINS, SELF, sw_stog_4_se_with_headlights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_stog_4_se;
    1: return spriteset_stog_4_headlights;
}

/* First car of vehicle is an stog_4_se. If it is the first car of the train, display headlights.  */
switch (FEAT_TRAINS, SELF, sw_stog_4_se_position, position_in_consist) {
	0: return sw_stog_4_se_with_headlights;
	return spriteset_stog_4_se;
}

/* Sprite stack for stog_4_se with marker lights */
switch (FEAT_TRAINS, SELF, sw_stog_4_se_with_marker_lights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_stog_4_se_reversed;
    1: return spriteset_stog_4_marker_lights;
}

/* Last car of vehicle is an stog_4_se. If it is the last car of the train, display marker lights.  */
switch (FEAT_TRAINS, SELF, sw_stog_4_se_reversed_position, position_in_consist_from_end) {
	0: return sw_stog_4_se_with_marker_lights;
	return spriteset_stog_4_se_reversed;
}

switch (FEAT_TRAINS, PARENT, sw_stog_4_reverser_1, vehicle_is_reversed) {
	0: spriteset_stog_4_sb;
	spriteset_stog_4_sc_reversed;
}

switch (FEAT_TRAINS, PARENT, sw_stog_4_reverser_2, vehicle_is_reversed) {
	0: spriteset_stog_4_sc;
	spriteset_stog_4_sb_reversed;
}

switch (FEAT_TRAINS, SELF, sw_stog_4_graphics_a, position_in_consist % 8) {
	0: sw_stog_4_se_position;
	1: spriteset_stog_4_sb;
	2: spriteset_stog_4_sc;
	3: spriteset_stog_4_sc;
	4: spriteset_stog_4_sc_reversed;
	5: spriteset_stog_4_sc_reversed;
	6: spriteset_stog_4_sb_reversed;
	7: sw_stog_4_se_reversed_position;
}

switch (FEAT_TRAINS, SELF, sw_stog_4_graphics_b, position_in_articulated_veh) {
	0: sw_stog_4_se_position;
	1: sw_stog_4_reverser_1;
	2: sw_stog_4_reverser_2;
	3: sw_stog_4_se_reversed_position;
}

/* Select the correct part of the train. */
switch (FEAT_TRAINS, SELF, sw_stog_4_graphics, num_vehs_in_vehid_chain % 8 == 0) {
	1: sw_stog_4_graphics_a;
	sw_stog_4_graphics_b;
}

/* Assemble a 4-car articulated vehicle. */
switch (FEAT_TRAINS, SELF, sw_stog_4_articulated_part, extra_callback_info1) {
	1..3: return item_stog_4;
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, sw_stog_4_visual_effect_and_powered, 
(((position_in_articulated_veh == 1) && !func_is_reversed()) || ((position_in_articulated_veh_from_end == 1) && func_is_reversed())) ) {
	1: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 0, ENABLE_WAGON_POWER);
	return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}

/* ITEM*/

item(FEAT_TRAINS, item_stog_4, 16) {
	property {
		/* Common properties */
		name: string(STR_NAME_STOG_4);
		climates_available: ALL_CLIMATES;
		introduction_date: date(1995, 01, 01);
		model_life:	40;
		vehicle_life: 40;
		reliability_decay: 20;
		refittable_cargo_classes: bitmask(CC_PASSENGERS);
		default_cargo_type: PASS;
		cargo_capacity: 45;
		loading_speed: 10;
		/* Train-specific properties */
		sprite_id: SPRITE_ID_NEW_TRAIN; //enable new graphics
		speed: 120 km/h;
		misc_flags: bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_SPRITE_STACK);
		refit_cost: 0;
		track_type:	ELRL;
		ai_special_flag: AI_FLAG_PASSENGER;
		running_cost_base: RUNNING_COST_STEAM;
		engine_class: ENGINE_CLASS_ELECTRIC;
		power: 860 kW;
		weight: 63 ton;
		tractive_effort_coefficient: 0.3;
		length: 4;
	}
	graphics {
		default: sw_stog_4_graphics;
		purchase: spriteset_stog_4_se;
		colour_mapping: sw_cc_red_red;
		articulated_part: sw_stog_4_articulated_part;
		cost_factor: 70;
		running_cost_factor: func_running_costs(2098);
		purchase_running_cost_factor: 2098;
		visual_effect_and_powered: sw_stog_4_visual_effect_and_powered;
		additional_text: string(STR_ROLE_URBAN_PASS);
	}
}

/* End S-Tog 4 */
