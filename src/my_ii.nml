
/* Begin MY (II) */

/* GRAPHICS */
spriteset(spriteset_my_ii_1, "src\gfx\my_ii_1_8bpp.png") {template_8_standard(0,0)}
spriteset(spriteset_my_ii_2, "src\gfx\my_ii_2_8bpp.png") {template_8_standard(0,0)}
spriteset(spriteset_my_ii_3, "src\gfx\my_ii_3_8bpp.png") {template_8_standard(0,0)}
spriteset(spriteset_my_ii_headlights, "src\gfx\my_headlights.png") {template_8_standard(0,0)}


/* SWITCHES */

switch (FEAT_TRAINS, SELF, sw_my_ii_livery_date, date_of_last_service) {
	0..date(1984, 1, 1): return 0;
	date(1984, 1, 2)..date(2004, 1, 1): return 1;
	return 2;
}

switch (FEAT_TRAINS, SELF, sw_my_ii_livery_type, param_livery_selection) {
	0: sw_my_ii_livery_date;
	return cargo_subtype;
}

/* Livery selection */
switch (FEAT_TRAINS, SELF, sw_my_ii_livery, sw_my_ii_livery_type()) {
	0: spriteset_my_ii_1;
	1: spriteset_my_ii_2;
	2: spriteset_my_ii_3;
}

/* Headlight sprite stacker.  */
switch (FEAT_TRAINS, SELF, sw_my_ii_with_headlights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sw_my_ii_livery;    // Return the base sprite
    1: return spriteset_my_ii_headlights;    // Return the overlay
}

/* If this loco is leading the train, display headlights.  */
switch (FEAT_TRAINS, SELF, sw_my_ii_position, position_in_consist) {
	0: return sw_my_ii_with_headlights;
	return sw_my_ii_livery;
}

/* Select the proper colour */
switch (FEAT_TRAINS, SELF, sw_colour_mapping, sw_my_ii_livery_type()) {
	0..1: return sw_cc_red_cream;
	2: return sw_cc_dark_blue_yellow;
}

/* Create livery refits with cargo_subtypes */
switch (FEAT_TRAINS, SELF, sw_my_ii_cargo_subtype_text, cargo_subtype) {
	0: return string(STR_LIVERY_BROWN);
	1: return string(STR_LIVERY_RED_BLACK);
	2: return string(STR_LIVERY_BLUE_YELLOW);
}

switch (FEAT_TRAINS, SELF, sw_my_ii_cargo_subtype, param_livery_selection) {
	0: return CB_RESULT_NO_TEXT;
	return sw_my_ii_cargo_subtype_text;
}

item(FEAT_TRAINS, item_my_ii, 8) {
	property {
		/* Common properties */
		name: string(STR_NAME_MY_II);
		climates_available: ALL_CLIMATES;
		introduction_date: date(1954,01,01);
		model_life:	40;
		vehicle_life: 40;
		reliability_decay: 20;
		refittable_cargo_classes: bitmask(CC_PASSENGERS);
		default_cargo_type: PASS;
		cargo_capacity: 1;
		/* Train-specific properties */
		sprite_id: SPRITE_ID_NEW_TRAIN; //enable new graphics
		speed: 133 km/h;
		misc_flags: bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		track_type:	RAIL;
		running_cost_base: RUNNING_COST_STEAM;
		engine_class: ENGINE_CLASS_DIESEL;
		power: 1950 hp;
		weight: 102 ton;
		tractive_effort_coefficient: 0.3;
		length: 8;
	}
	graphics {
		default: sw_my_ii_position;
		colour_mapping: sw_colour_mapping;
		cargo_subtype_text: sw_my_ii_cargo_subtype;
		cost_factor: 57;
		running_cost_factor: func_running_costs(2167);
		purchase_running_cost_factor: 2167;
		additional_text: string(STR_ROLE_PASS_FREIGHT);
		cargo_capacity: 0;
	}
}

/* End MY (II) */
