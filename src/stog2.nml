
/* Begin S-Tog 2 */

/* GRAPHICS */

/* Lights */
spriteset(spriteset_stog_2_headlights, "src\gfx\stog_2_headlights.png") {template_8_standard(0,0)}
spriteset(spriteset_stog_2_marker_lights, "src\gfx\stog_2_marker_lights.png") {template_8_reversed(0,0)}

spriteset(spriteset_stog_2_mm, "src\gfx\stog_2_mm_8bpp.png") {template_8_standard(0,0)}
spriteset(spriteset_stog_2_mm_reversed, "src\gfx\stog_2_mm_8bpp.png") {template_8_reversed(0,0)}

spriteset(spriteset_stog_2_fs, "src\gfx\stog_2_fs_8bpp.png") {template_8_standard(0,0)}
spriteset(spriteset_stog_2_fs_reversed, "src\gfx\stog_2_fs_8bpp.png") {template_8_reversed(0,0)}

/* SWITCHES */

/* Sprite stack for stog_2_mm with headlights */
switch (FEAT_TRAINS, SELF, sw_stog_2_mm_with_headlights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_stog_2_mm;    // Return the base sprite
    1: return spriteset_stog_2_headlights;    // Return the overlay
}

/* First car of vehicle is an stog_2_mm. If it is the first car of the train, display headlights.  */
switch (FEAT_TRAINS, SELF, sw_stog_2_mm_position, position_in_consist) {
	0: return sw_stog_2_mm_with_headlights;
	return spriteset_stog_2_mm;
}

/* Sprite stack for stog_2_mm with marker lights */
switch (FEAT_TRAINS, SELF, sw_stog_2_mm_with_marker_lights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_stog_2_mm_reversed;    // Return the base sprite
    1: return spriteset_stog_2_marker_lights;    // Return the overlay
}

/* Last car of vehicle is an stog_2_mm. If it is the last car of the train, display marker lights.  */
switch (FEAT_TRAINS, SELF, sw_stog_2_mm_reversed_position, position_in_consist_from_end) {
	0: return sw_stog_2_mm_with_marker_lights;
	return spriteset_stog_2_mm_reversed;
}

/* Sprite stack for stog_2_fs with headlights */
switch (FEAT_TRAINS, SELF, sw_stog_2_fs_with_headlights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_stog_2_fs;    // Return the base sprite
    1: return spriteset_stog_2_headlights;    // Return the overlay
}

/* First car of vehicle is an stog_2_fs. If it is the first car of the train, display headlights.  */
switch (FEAT_TRAINS, SELF, sw_stog_2_fs_position, position_in_consist) {
	0: return sw_stog_2_fs_with_headlights;
	return spriteset_stog_2_fs;
}

/* Sprite stack for stog_2_fs with marker lights */
switch (FEAT_TRAINS, SELF, sw_stog_2_fs_with_marker_lights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_stog_2_fs_reversed;    // Return the base sprite
    1: return spriteset_stog_2_marker_lights;    // Return the overlay
}

/* Last car of vehicle is an stog_2_fs. If it is the last car of the train, display marker lights.  */
switch (FEAT_TRAINS, SELF, sw_stog_2_fs_reversed_position, position_in_consist_from_end) {
	0: return sw_stog_2_fs_with_marker_lights;
	return spriteset_stog_2_fs_reversed;
}

/* Consist A - 2 or 6 cars */
switch (FEAT_TRAINS, PARENT, sw_stog_2_reverser_0, vehicle_is_reversed) {
	0: sw_stog_2_mm_position;
	sw_stog_2_fs_position;
}

switch (FEAT_TRAINS, PARENT, sw_stog_2_reverser_1, vehicle_is_reversed) {
	0: sw_stog_2_fs_reversed_position;
	sw_stog_2_mm_reversed_position;
}

/* Select the correct part of the train. */
switch (FEAT_TRAINS, SELF, sw_stog_2_graphics, position_in_articulated_veh) {
	0: sw_stog_2_reverser_0;
	1: sw_stog_2_reverser_1;
}

/* Assemble a 2-car articulated vehicle. */
switch (FEAT_TRAINS, SELF, sw_stog_2_articulated_part, extra_callback_info1) {
	1: return item_stog_2;
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, sw_stog_2_visual_effect_and_powered, 
(((position_in_articulated_veh == 0) && !func_is_reversed()) || ((position_in_articulated_veh_from_end == 0) && func_is_reversed())) ) {
	1: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 0, ENABLE_WAGON_POWER);
	return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}

/* ITEM*/

item(FEAT_TRAINS, item_stog_2, 14) {
	property {
		/* Common properties */
		name: string(STR_NAME_STOG_2);
		climates_available: ALL_CLIMATES;
		introduction_date: date(1967, 01, 01);
		model_life:	40;
		vehicle_life: 40;
		reliability_decay: 20;
		refittable_cargo_classes: bitmask(CC_PASSENGERS);
		default_cargo_type: PASS;
		cargo_capacity: 65;
		loading_speed: 10;
		/* Train-specific properties */
		sprite_id: SPRITE_ID_NEW_TRAIN; //enable new graphics
		speed: 100 km/h;
		misc_flags: bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_SPRITE_STACK);
		refit_cost: 0;
		track_type:	ELRL;
		ai_special_flag: AI_FLAG_PASSENGER;
		running_cost_base: RUNNING_COST_STEAM;
		engine_class: ENGINE_CLASS_ELECTRIC;
		power: 400 kW;
		weight: 82 ton;
		tractive_effort_coefficient: 0.3;
		length: 8;
	}
	graphics {
		default: sw_stog_2_graphics;
		purchase: spriteset_stog_2_mm;
		colour_mapping: sw_cc_red_red;
		articulated_part: sw_stog_2_articulated_part;
		cost_factor: 36;
		running_cost_factor: func_running_costs(1403);
		purchase_running_cost_factor: 1403;
		visual_effect_and_powered: sw_stog_2_visual_effect_and_powered;
	}
}

/* End S-Tog 2 */
